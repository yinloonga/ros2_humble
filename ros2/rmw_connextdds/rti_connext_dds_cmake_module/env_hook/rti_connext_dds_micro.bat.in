set "_CONNEXTDDSMICRO_DIR_BUILD=@RTIMEHOME_NATIVE@"
set "_CONNEXTDDSMICRO_ARCH=@RTIME_TARGET_NAME@"

:: Inspect environment for a Connext Micro 3.x installation.
set "_CONNEXTDDSMICRO_DIR_ENV="
if "%RTIMEHOME%" NEQ "" (
  set "_CONNEXTDDSMICRO_DIR_ENV=%RTIMEHOME%"
  set "_CONNEXTDDSMICRO_DIR_ENV_SRC=RTIMEHOME"
)

:: If Connext Micro installation used at build-time is still available, use that one,
:: otherwise inspect environment to see if another one is available.
set "_CONNEXTDDSMICRO_DIR_EXPORT="
if exist "%_CONNEXTDDSMICRO_DIR_BUILD%" (
  if "%CONNEXTDDSMICRO_IGNORE_BUILD%" == "" (
    :: Warn about unused environment variable.
    if "%_CONNEXTDDSMICRO_DIR_ENV%" NEQ "" (
      if "%_CONNEXTDDSMICRO_DIR_ENV%" NEQ "%_CONNEXTDDSMICRO_DIR_BUILD%" (
        echo "[@PROJECT_NAME@][warning] Using RTI Connext DDS Micro installation configured during build-time (%_CONNEXTDDSMICRO_DIR_BUILD%). The installation specified via %_CONNEXTDDSMICRO_DIR_ENV_SRC% (%_CONNEXTDDSMICRO_DIR_ENV%) will be ignored. Use variable CONNEXTDDSMICRO_IGNORE_BUILD to override this behavior." 1>&2
      )
    )
    set "_CONNEXTDDSMICRO_DIR_EXPORT=%_CONNEXTDDSMICRO_DIR_BUILD%"
  )
)

if "%_CONNEXTDDSMICRO_DIR_EXPORT%" == "" (
  set "_CONNEXTDDSMICRO_DIR_EXPORT=%_CONNEXTDDSMICRO_DIR_ENV%"
)

set "_CONNEXTDDSMICRO_LIB=%_CONNEXTDDSMICRO_DIR_EXPORT%\lib\%_CONNEXTDDSMICRO_ARCH%"

set "_CONNEXTDDSMICRO_DIR_EXPORT_ERROR="
if "%_CONNEXTDDSMICRO_DIR_EXPORT%" == "" (
  set "_CONNEXTDDSMICRO_DIR_EXPORT_ERROR=No RTI Connext DDS Micro installation specified"
)
if not exist "%_CONNEXTDDSMICRO_DIR_EXPORT%" (
  set "_CONNEXTDDSMICRO_DIR_EXPORT_ERROR=RTI Connext DDS Micro installation not found (%_CONNEXTDDSMICRO_DIR_EXPORT%)"
)
if not exist "%_CONNEXTDDSMICRO_LIB%" (
  set "_CONNEXTDDSMICRO_DIR_EXPORT_ERROR=RTI Connext DDS Micro library directory not found (%_CONNEXTDDSMICRO_LIB%)"
)
if "%_CONNEXTDDSMICRO_DIR_EXPORT_ERROR%" NEQ "" (
  echo "[@PROJECT_NAME@][warning] %_CONNEXTDDSMICRO_DIR_EXPORT_ERROR%. RTI Connext DDS Micro will not be available at runtime, unless you already configured PATH manually." 1>&2
  goto cleanup_env
)

:: Add RTI Connext DDS Micro to the environment.
set "Path=%path:"=%"
set "Path=%_CONNEXTDDSMICRO_LIB%;%Path%"

set "RTIMEHOME=%_CONNEXTDDSMICRO_DIR_EXPORT%"
set "RTIME_TARGET_NAME=%_CONNEXTDDSMICRO_ARCH%"

:cleanup_env
  set "_CONNEXTDDSMICRO_DIR_BUILD="
  set "_CONNEXTDDSMICRO_ARCH="
  set "_CONNEXTDDSMICRO_DIR_ENV="
  set "_CONNEXTDDSMICRO_DIR_ENV_SRC="
  set "_CONNEXTDDSMICRO_DIR_EXPORT="
  set "_CONNEXTDDSMICRO_DIR_EXPORT_ERROR="
  set "_CONNEXTDDSMICRO_LIB="
goto :eof
